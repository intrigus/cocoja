apply plugin: 'maven'
apply plugin: 'signing'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar, file: file('libs/cocoja-natives.jar'), name: 'cocoja-natives', type: 'jar'
}

javadoc {
    onlyIf { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                url 'https://github.com/intrigus/cocoja/'

                scm {
                    connection 'scm:git:git@github.com:intrigus/cocoja.git'
                    developerConnection 'scm:git:git@github.com:intrigus/cocoja.git'
                    url 'git@github.com:intrigus/cocoja.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'intrigus'
                        name 'Simon Gerst'
                        url 'https://github.com/intrigus'
                    }
                }
            }
            addFilter('cocoja') {artifact, file ->
                artifact.name == 'cocoja'
            }
            addFilter('cocoja-natives') {artifact, file ->
                artifact.name == 'cocoja-natives'
            }            
        }
    }
}